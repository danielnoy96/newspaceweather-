
struct Sim {
    colours: f32,
    beta: f32,
    rMax: f32,
    force: f32,
    friction: f32,
    dt: f32,
    cellSize: f32,
    cellAmt: f32,
    avoidance: f32,
    worldSize: f32,
    border: f32,
    vortex: f32,
}

struct Particle {
    pos: vec2f,
    vel: vec2f,
    colour: f32,
}

struct ListParticle {
    idx: f32,
    pos: vec2f,
    vel: vec2f,
    colour: f32,
    next: u32,
}

struct Heads {
    num: atomic<u32>,
    data: array<atomic<u32>>
}

struct LinkedList {
    data: array<ListParticle>
}

@group(0) @binding(0) var<uniform> sim: Sim;

@group(0) @binding(1) var<storage, read> input: array<Particle>;

@group(0) @binding(2) var<storage, read_write> heads: Heads;
@group(0) @binding(3) var<storage, read_write> linkedList: LinkedList;

fn hash3i(k: vec3<i32>) -> u32 {
    let offset: u32 = 0x80000000u;
    var x: u32 = (u32(k.x) + offset) * 0x9E3779B1u;
    var y: u32 = (u32(k.y) + offset) * 0x85EBCA6Bu;
    var z: u32 = (u32(k.z) + offset) * 0xC2B2AE35u;

    var h: u32 = x ^ y ^ z;

    h ^= h >> 16u;
    h *= 0x7FEB352Du;
    h ^= h >> 15u;
    h *= 0x846CA68Bu;
    h ^= h >> 16u;

    return h;
}

@compute @workgroup_size(64)
fn main(@builtin(global_invocation_id) global_id: vec3<u32>) {
    if (global_id.x >= arrayLength(&input)) {
        return;
    }
    var p  = input[global_id.x];

    let gridPos = vec2i(floor(p.pos / sim.cellSize));

    let cellHash = hash3i(vec3i(gridPos, 0)) % arrayLength(&heads.data);

    let listIndex = atomicAdd(&heads.num, 1u);

    if (listIndex < arrayLength(&input)) {
        let lastHead = atomicExchange(&heads.data[cellHash], listIndex);
        linkedList.data[listIndex].idx = f32(global_id.x);
        linkedList.data[listIndex].pos = p.pos;
        linkedList.data[listIndex].vel = p.vel;
        linkedList.data[listIndex].colour = p.colour;
        linkedList.data[listIndex].next = lastHead;
    }
}